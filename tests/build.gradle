plugins {
    id 'java'
    id "de.undercouch.download" version "5.4.0"
}

group = 'org.example'
version = '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.ballerinalang:ballerina-lang:${ballerinaLangVersion}"

    testImplementation 'org.testng:testng:7.7.0'
}

test {
    systemProperty "ballerina.home", "build/ballerina-${ballerinaLangVersion}-swan-lake/" +
            "ballerina-${ballerinaLangVersion}-swan-lake/distributions/ballerina-${ballerinaLangVersion}"
    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }
}

test.dependsOn(":mi-ballerina:build")
test.dependsOn("unzipFile")

task downloadBallerina(type: Download) {
    src([
            "https://dist.ballerina.io/downloads/${ballerinaLangVersion}/" +
                    "ballerina-${ballerinaLangVersion}-swan-lake.zip"
    ])
    dest file("build")
    overwrite false
}

task unzipFile(type: Copy) {
    dependsOn("downloadBallerina")
    from(zipTree("build/ballerina-${ballerinaLangVersion}-swan-lake.zip"))
    into("build/ballerina-${ballerinaLangVersion}-swan-lake")
}
